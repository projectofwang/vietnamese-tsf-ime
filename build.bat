@echo off
setlocal enabledelayedexpansion

:: Locate VS installation path
for /f "usebackq tokens=*" %%i in (`"C:\Program Files (x86)\Microsoft Visual Studio\Installer\vswhere.exe" -latest -property installationPath`) do (
    set "VS_PATH=%%i"
)

if not defined VS_PATH (
    echo Error: Visual Studio installation not found.
    exit /b 1
)

set "VCVARS=!VS_PATH!\VC\Auxiliary\Build\vcvarsall.bat"
if not exist "!VCVARS!" (
    echo Error: vcvarsall.bat not found at !VCVARS!
    exit /b 1
)

if not "%~1"=="" if /I not "%~1"=="arm64" (
    echo Usage: build.bat [arm64]
    exit /b 2
)

if not defined OUT_DIR set "OUT_DIR=build"

:: The numbers of neokey_config.exe's version resource, from VERSION:
:: "0.1.20-dev" gives 0, 1 and 20.
set "VER_MAJOR=0"
set "VER_MINOR=0"
set "VER_PATCH=0"
if exist "%~dp0VERSION" (
    for /f "usebackq tokens=1-3 delims=.-+ " %%a in ("%~dp0VERSION") do (
        set "VER_MAJOR=%%a"
        set "VER_MINOR=%%b"
        set "VER_PATCH=%%c"
    )
)
set "RC_VERSION=/d NEOKEY_VERSION_MAJOR=!VER_MAJOR! /d NEOKEY_VERSION_MINOR=!VER_MINOR! /d NEOKEY_VERSION_PATCH=!VER_PATCH!"

if /I "%~1"=="arm64" goto build_arm64

set "OBJ_X64=!OUT_DIR!\x64"
set "OBJ_X86=!OUT_DIR!\x86"
set "OBJ_TEST_X86=!OUT_DIR!\test-x86"

:: Create build directories
if not exist "!OUT_DIR!" mkdir "!OUT_DIR!"
if not exist "!OBJ_X64!" mkdir "!OBJ_X64!"
if not exist "!OBJ_X86!" mkdir "!OBJ_X86!"
if not exist "!OBJ_TEST_X86!" mkdir "!OBJ_TEST_X86!"

:: Clean up old files
del /q *.obj "!OUT_DIR!\*.obj" "!OUT_DIR!\*.lib" "!OUT_DIR!\*.exp" 2>nul

echo =========================================
echo Building 64-bit components...
echo =========================================
cmd.exe /c "call "!VCVARS!" amd64 && rc.exe /nologo /c65001 !RC_VERSION! /fo !OUT_DIR!\resources.res /i src\config-app src\config-app\resources.rc"
if errorlevel 1 exit /b 1

cmd.exe /c "call "!VCVARS!" amd64 && cl.exe /nologo /std:c++latest /utf-8 /EHsc /MT /O2 /guard:cf /LD /Isrc/shared /Isrc/ime-dll /Isrc/core /Fo"!OBJ_X64!\\" /Fe!OUT_DIR!\neokey.dll src\ime-dll\dllmain.cpp src\ime-dll\ime_processor.cpp src\ime-dll\register.cpp src\ime-dll\fake_backspace_handler.cpp src\core\rules.cpp src\core\engine.cpp src\core\free_typing.cpp src\core\free_typing_repair.cpp src\core\speller.cpp src\core\fuzzy_input.cpp src\shared\logger.cpp src\shared\tray_ipc.cpp uuid.lib ole32.lib oleaut32.lib user32.lib advapi32.lib comctl32.lib /link /def:src\ime-dll\neokey.def /guard:cf /DYNAMICBASE /NXCOMPAT"
if errorlevel 1 exit /b 1

cmd.exe /c "call "!VCVARS!" amd64 && cl.exe /nologo /std:c++latest /utf-8 /EHsc /MT /O2 /guard:cf /Isrc/shared /Isrc/ime-dll /Isrc/core /Fo"!OBJ_X64!\\" /Fe!OUT_DIR!\neokey_config.exe src\config-app\main.cpp src\config-app\setup_commands.cpp src\config-app\setup_actions.cpp src\shared\logger.cpp src\shared\tray_ipc.cpp !OUT_DIR!\resources.res /link /subsystem:windows comctl32.lib advapi32.lib user32.lib comdlg32.lib gdi32.lib gdiplus.lib shell32.lib dwmapi.lib uxtheme.lib winhttp.lib bcrypt.lib /guard:cf /DYNAMICBASE /NXCOMPAT"
if errorlevel 1 exit /b 1

cmd.exe /c "call "!VCVARS!" amd64 && cl.exe /nologo /std:c++latest /utf-8 /EHsc /MT /O2 /guard:cf /Isrc/shared /Isrc/config-app /Isrc/core /Isrc/ime-dll /Fo"!OBJ_X64!\\" /Fe!OUT_DIR!\setup_tests.exe tests\setup_tests.cpp src\config-app\setup_commands.cpp src\config-app\setup_actions.cpp advapi32.lib user32.lib shell32.lib bcrypt.lib /link /guard:cf /DYNAMICBASE /NXCOMPAT"
if errorlevel 1 exit /b 1

cmd.exe /c "call "!VCVARS!" amd64 && cl.exe /nologo /std:c++latest /utf-8 /EHsc /MT /O2 /guard:cf /Isrc/core /Isrc/shared /Isrc/ime-dll /Fo"!OBJ_X64!\\" /Fe!OUT_DIR!\core_tests.exe tests\core_tests.cpp src\ime-dll\fake_backspace_handler.cpp src\core\rules.cpp src\core\engine.cpp src\core\free_typing.cpp src\core\free_typing_repair.cpp src\core\speller.cpp src\core\fuzzy_input.cpp src\shared\logger.cpp src\shared\tray_ipc.cpp advapi32.lib user32.lib /link /guard:cf /DYNAMICBASE /NXCOMPAT"
if errorlevel 1 exit /b 1

echo =========================================
echo Building 32-bit components...
echo =========================================
cmd.exe /c "call "!VCVARS!" x86 && cl.exe /nologo /std:c++latest /utf-8 /EHsc /MT /O2 /guard:cf /LD /Isrc/shared /Isrc/ime-dll /Isrc/core /Fo"!OBJ_X86!\\" /Fe!OUT_DIR!\neokey32.dll src\ime-dll\dllmain.cpp src\ime-dll\ime_processor.cpp src\ime-dll\register.cpp src\ime-dll\fake_backspace_handler.cpp src\core\rules.cpp src\core\engine.cpp src\core\free_typing.cpp src\core\free_typing_repair.cpp src\core\speller.cpp src\core\fuzzy_input.cpp src\shared\logger.cpp src\shared\tray_ipc.cpp uuid.lib ole32.lib oleaut32.lib user32.lib advapi32.lib comctl32.lib /link /def:src\ime-dll\neokey.def /guard:cf /DYNAMICBASE /NXCOMPAT"
if errorlevel 1 exit /b 1

cmd.exe /c "call "!VCVARS!" x86 && cl.exe /nologo /std:c++latest /utf-8 /EHsc /MT /O2 /guard:cf /Isrc/core /Isrc/shared /Isrc/ime-dll /Fo"!OBJ_TEST_X86!\\" /Fe!OUT_DIR!\core_tests32.exe tests\core_tests.cpp src\ime-dll\fake_backspace_handler.cpp src\core\rules.cpp src\core\engine.cpp src\core\free_typing.cpp src\core\free_typing_repair.cpp src\core\speller.cpp src\core\fuzzy_input.cpp src\shared\logger.cpp src\shared\tray_ipc.cpp advapi32.lib user32.lib /link /guard:cf /DYNAMICBASE /NXCOMPAT"
if errorlevel 1 exit /b 1

goto build_complete

:build_arm64
set "OBJ_ARM64=!OUT_DIR!\arm64"
set "OBJ_CONFIG_ARM64=!OUT_DIR!\config-arm64"
set "OBJ_TEST_ARM64=!OUT_DIR!\test-arm64"

if not exist "!OUT_DIR!" mkdir "!OUT_DIR!"
if not exist "!OBJ_ARM64!" mkdir "!OBJ_ARM64!"
if not exist "!OBJ_CONFIG_ARM64!" mkdir "!OBJ_CONFIG_ARM64!"
if not exist "!OBJ_TEST_ARM64!" mkdir "!OBJ_TEST_ARM64!"

del /q *.obj "!OUT_DIR!\*.obj" "!OUT_DIR!\*.lib" "!OUT_DIR!\*.exp" 2>nul

echo =========================================
echo Building ARM64 preview components...
echo =========================================
cmd.exe /c "call "!VCVARS!" amd64_arm64 && rc.exe /nologo /c65001 !RC_VERSION! /fo !OUT_DIR!\resources_arm64.res /i src\config-app src\config-app\resources.rc"
if errorlevel 1 exit /b 1

cmd.exe /c "call "!VCVARS!" amd64_arm64 && cl.exe /nologo /std:c++latest /utf-8 /EHsc /MT /O2 /guard:cf /LD /Isrc/shared /Isrc/ime-dll /Isrc/core /Fo"!OBJ_ARM64!\\" /Fe!OUT_DIR!\neokey_arm64.dll src\ime-dll\dllmain.cpp src\ime-dll\ime_processor.cpp src\ime-dll\register.cpp src\ime-dll\fake_backspace_handler.cpp src\core\rules.cpp src\core\engine.cpp src\core\free_typing.cpp src\core\free_typing_repair.cpp src\core\speller.cpp src\core\fuzzy_input.cpp src\shared\logger.cpp src\shared\tray_ipc.cpp uuid.lib ole32.lib oleaut32.lib user32.lib advapi32.lib comctl32.lib /link /def:src\ime-dll\neokey.def /guard:cf /DYNAMICBASE /NXCOMPAT"
if errorlevel 1 exit /b 1

cmd.exe /c "call "!VCVARS!" amd64_arm64 && cl.exe /nologo /std:c++latest /utf-8 /EHsc /MT /O2 /guard:cf /Isrc/shared /Isrc/ime-dll /Isrc/core /Fo"!OBJ_CONFIG_ARM64!\\" /Fe!OUT_DIR!\neokey_config_arm64.exe src\config-app\main.cpp src\config-app\setup_commands.cpp src\config-app\setup_actions.cpp src\shared\logger.cpp src\shared\tray_ipc.cpp !OUT_DIR!\resources_arm64.res /link /subsystem:windows comctl32.lib advapi32.lib user32.lib comdlg32.lib gdi32.lib gdiplus.lib shell32.lib dwmapi.lib uxtheme.lib winhttp.lib bcrypt.lib /guard:cf /DYNAMICBASE /NXCOMPAT"
if errorlevel 1 exit /b 1

cmd.exe /c "call "!VCVARS!" amd64_arm64 && cl.exe /nologo /std:c++latest /utf-8 /EHsc /MT /O2 /guard:cf /Isrc/core /Isrc/shared /Isrc/ime-dll /Fo"!OBJ_TEST_ARM64!\\" /Fe!OUT_DIR!\core_tests_arm64.exe tests\core_tests.cpp src\ime-dll\fake_backspace_handler.cpp src\core\rules.cpp src\core\engine.cpp src\core\free_typing.cpp src\core\free_typing_repair.cpp src\core\speller.cpp src\core\fuzzy_input.cpp src\shared\logger.cpp src\shared\tray_ipc.cpp advapi32.lib user32.lib /link /guard:cf /DYNAMICBASE /NXCOMPAT"
if errorlevel 1 exit /b 1

:build_complete
:: Clean up temp obj files in root if any
del /q *.obj 2>nul

echo Build complete!
exit /b 0
